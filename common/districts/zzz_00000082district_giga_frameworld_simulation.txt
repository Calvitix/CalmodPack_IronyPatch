@advanced_cost = 1000
@advanced_rare_cost = 50
@advanced_upkeep = 5
@advanced_rare_upkeep = 1
@advanced_time = 720
district_giga_frameworld_simulation = {
    base_buildtime = @advanced_time
    icon = district_arcology_administrative
    is_capped_by_modifier = no
    show_on_uncolonized = {
        exists = from
        from = {
            is_machine_empire = yes
            NOT = {
                has_valid_civic = civic_machine_servitor
            }
            has_technology = "tech_colonial_bureaucracy"
        }
        uses_district_set = giga_frameworld
    }
    potential = {
        exists = owner
        owner = {
            is_machine_empire = yes
            NOT = {
                has_valid_civic = civic_machine_servitor
            }
            has_technology = "tech_colonial_bureaucracy"
        }
        uses_district_set = giga_frameworld
    }
    prerequisites = {
        tech_colonial_bureaucracy
    }
    show_tech_unlock_if = {
        has_origin = origin_frameworld
        not = {
            has_valid_civic = civic_machine_servitor
        }
    }
    conversion_ratio = 1
    convert_to = {
        district_giga_frameworld_administrative
        district_giga_frameworld_temple
        district_giga_frameworld_synapse
        district_giga_frameworld_sanctuary
    }
    resources = {
        category = planet_districts_hab_research
        cost = {
            minerals = @advanced_cost
            rare_crystals = @advanced_rare_cost
        }
        upkeep = {
            energy = @advanced_upkeep
            rare_crystals = @advanced_rare_upkeep
        }
    }
    planet_modifier = {
        planet_housing_add = 2
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                is_machine_empire = yes
            }
        }
        modifier = {
            job_coordinator_add = 4
        }
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                is_machine_empire = yes
            }
        }
        text = job_coordinator_effect_desc
    }
    ai_resource_production = {
        unity = 1
        trigger = {
            always = yes
        }
    }
}

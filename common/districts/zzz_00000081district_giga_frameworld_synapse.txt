@advanced_cost = 1000
@advanced_rare_cost = 50
@advanced_upkeep = 5
@advanced_rare_upkeep = 1
@advanced_time = 720
district_giga_frameworld_synapse = {
    base_buildtime = @advanced_time
    icon = district_arcology_administrative
    is_capped_by_modifier = no
    show_on_uncolonized = {
        exists = from
        from = {
            is_hive_empire = yes
            has_technology = "tech_hive_cluster"
        }
        uses_district_set = giga_frameworld
    }
    potential = {
        exists = owner
        owner = {
            is_hive_empire = yes
            has_technology = "tech_hive_cluster"
        }
        uses_district_set = giga_frameworld
    }
    conversion_ratio = 1
    convert_to = {
        district_giga_frameworld_simulation
        district_giga_frameworld_administrative
        district_giga_frameworld_temple
        district_giga_frameworld_sanctuary
    }
    resources = {
        category = planet_districts
        cost = {
            minerals = @advanced_cost
            exotic_gases = @advanced_rare_cost
        }
        upkeep = {
            energy = @advanced_upkeep
            exotic_gases = @advanced_rare_upkeep
        }
    }
    planet_modifier = {
        planet_housing_add = 2
    }
    planet_modifier = {
        job_synapse_drone_add = 4
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_active_tradition = tr_domination_synaptic_extensions
            }
        }
        modifier = {
            planet_housing_add = 2
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_edict = synaptic_reinforcement
            }
        }
        job_maintenance_drone_add = 1
    }
    triggered_desc = {
        text = job_synapse_drone_effect_desc
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                has_edict = synaptic_reinforcement
            }
        }
        text = job_maintenance_drone_effect_desc
    }
    ai_resource_production = {
        unity = 1
        trigger = {
            always = yes
        }
    }
}

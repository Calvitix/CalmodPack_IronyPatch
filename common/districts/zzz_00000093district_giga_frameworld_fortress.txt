@advanced_cost = 1000
@advanced_rare_cost = 50
@advanced_upkeep = 5
@advanced_rare_upkeep = 1
@advanced_time = 720
district_giga_frameworld_fortress = {
    base_buildtime = @advanced_time
    icon = district_maginot_world_bunkers
    is_capped_by_modifier = no
    show_on_uncolonized = {
        exists = from
        from = {
            has_technology = "tech_global_defense_grid"
        }
        uses_district_set = giga_frameworld
    }
    potential = {
        exists = owner
        owner = {
            has_technology = "tech_global_defense_grid"
        }
        uses_district_set = giga_frameworld
    }
    prerequisites = {
        tech_global_defense_grid
    }
    show_tech_unlock_if = {
        has_origin = origin_frameworld
    }
    resources = {
        category = planet_districts
        produces = {
            trigger = {
                exists = owner
                owner = {
                    has_active_tradition = tr_unyielding_resistance_is_frugal
                }
            }
            unity = 3
        }
        cost = {
            minerals = @advanced_cost
            volatile_motes = @advanced_rare_cost
        }
        upkeep = {
            energy = @advanced_upkeep
            volatile_motes = @advanced_rare_upkeep
        }
    }
    planet_modifier = {
        planet_housing_add = 2
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                is_regular_empire = yes
            }
        }
        modifier = {
            job_soldier_add = 4
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                is_gestalt = yes
            }
        }
        modifier = {
            job_warrior_drone_add = 4
        }
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                is_regular_empire = yes
            }
        }
        text = job_soldier_effect_desc
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                is_gestalt = yes
            }
        }
        text = job_warrior_drone_effect_desc
    }
    triggered_desc = {
        trigger = {
            exists = owner
            is_giga_maginot_frameworld = yes
        }
        text = job_maginot_planetary_bunker_effect_desc
    }
}

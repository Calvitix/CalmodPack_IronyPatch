manager = {
    category = specialist
    condition_string = SPECIALIST_JOB_TRIGGER
    building_icon = building_autochthon_monument
    clothes_texture_index = 3
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }
    possible_precalc = can_fill_specialist_job
    triggered_planet_modifier = {
        potential = {
            always = yes
        }
        trade_value_add = 2
        mult = value:scripted_modifier_mult|MODIFIER|pop_job_trade_mult|
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_valid_civic = civic_efficiency_expert
            }
        }
        trade_value_add = 2
    }
    triggered_planet_modifier = {
        potential = {
            has_trait = trait_thrifty
            exists = owner
            owner = {
                has_valid_civic = civic_efficiency_expert
            }
        }
        trade_value_add = 0.5
    }
    triggered_planet_modifier = {
        potential = {
            has_trait = trait_nuumismatic_administration
            exists = owner
            owner = {
                has_valid_civic = civic_efficiency_expert
            }
        }
        trade_value_add = 0.5
    }
    resources = {
        category = planet_managers
        produces = {
            unity = 4
        }
        produces = {
            trigger = {
                exists = owner
                owner = {
                    has_valid_civic = civic_efficiency_expert
                }
            }
            unity = 1
        }
        upkeep = {
            consumer_goods = 2
        }
    }
    weight = {
        weight = @specialist_job_weight
        mult = value:job_weights_modifier|JOB|manager|RESOURCE|unity|
        mult = value:scripted_modifier_job_weight_mult|MODIFIER|pop_job_trade_mult|
        modifier = {
            factor = 0.2
            has_citizenship_rights = no
            NOT = {
                has_trait = trait_mechanical
            }
        }
        modifier = {
            factor = 0.1
            can_take_servant_job = yes
        }
        modifier = {
            factor = 1.5
            has_job = manager
        }
        modifier = {
            factor = 0.1
            is_enslaved = yes
            has_slavery_type = {
                type = slavery_indentured
            }
        }
    }
}

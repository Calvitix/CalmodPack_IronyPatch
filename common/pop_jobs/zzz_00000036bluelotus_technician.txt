bluelotus_technician = {
    category = specialist
    is_capped_by_modifier = yes
    condition_string = WORKER_JOB_TRIGGER
    building_icon = building_illicit_research_labs
    icon = foundry
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }
    possible_precalc = can_fill_worker_job
    resources = {
        category = planet_metallurgists
        produces = {
            alloys = 4
            waste = 1
        }
        upkeep = {
            trigger = {
                is_organic_species = yes
                is_phototrophic = no
                is_lithoid = no
            }
            food = @living_standard_food_normal
        }
        upkeep = {
            trigger = {
                has_phototrophic_energy_upkeep = yes
            }
            energy = @living_standard_phototrophic_normal
        }
        upkeep = {
            trigger = {
                is_phototrophic = yes
            }
            food = @living_standard_phototrophic_normal
        }
        upkeep = {
            trigger = {
                is_lithoid = yes
            }
            minerals = @living_standard_food_normal
        }
        upkeep = {
            trigger = {
                is_robotic_species = yes
            }
            energy = @living_standard_energy_normal
        }
    }
    weight = {
        weight = @specialist_job_weight
        mult = value:job_weights_modifier|JOB|bluelotus_technician|RESOURCE|alloys|
        modifier = {
            factor = 0.2
            has_citizenship_rights = no
            NOT = {
                has_trait = trait_mechanical
            }
        }
        modifier = {
            factor = 0.1
            can_take_servant_job = yes
        }
        modifier = {
            factor = 5
            has_job = bluelotus_technician
        }
        modifier = {
            factor = 1.2
            is_enslaved = yes
            has_slavery_type = {
                type = slavery_indentured
            }
        }
    }
}

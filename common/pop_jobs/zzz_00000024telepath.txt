telepath = {
    category = specialist
    condition_string = SPECIALIST_JOB_TRIGGER
    building_icon = building_psi_corps
    clothes_texture_index = 5
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }
    possible_precalc = can_fill_specialist_job
    possible = {
        OR = {
            has_trait = trait_latent_psionic
            has_trait = trait_psionic
        }
    }
    resources = {
        category = planet_telepaths
        produces = {
            unity = 3
        }
        produces = {
            trigger = {
                exists = owner
                owner = {
                    has_active_tradition = tr_domination_judgment_corps
                }
            }
            unity = 1
        }
        upkeep = {
            energy = 1
        }
        upkeep = {
            trigger = {
                exists = planet
                planet = {
                    has_modifier = anticrime_campaign
                }
            }
            energy = 2
        }
    }
    planet_modifier = {
        planet_crime_add = -35
        planet_jobs_produces_mult = 0.05
    }
    triggered_planet_modifier = {
        potential = {
            exists = planet
            planet = {
                has_modifier = anticrime_campaign
            }
        }
        modifier = {
            planet_crime_add = -10
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_edict = thought_enforcement
            }
        }
        modifier = {
            planet_crime_add = -5
        }
    }
    weight = {
        weight = @specialist_job_weight
        mult = 2
        mult = value:job_weights_modifier|JOB|telepath|RESOURCE|unity|
        modifier = {
            factor = 0.2
            has_citizenship_rights = no
        }
        modifier = {
            factor = 2
            has_trait = trait_resilient
        }
        modifier = {
            factor = 0.1
            can_take_servant_job = yes
        }
        modifier = {
            factor = 1.5
            has_job = telepath
        }
        modifier = {
            factor = 0.1
            is_enslaved = yes
            has_slavery_type = {
                type = slavery_indentured
            }
        }
    }
}

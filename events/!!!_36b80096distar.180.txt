namespace = distar
ship_event = {
    id = distar.180
    title = "distar.180.name"
    desc = "distar.180.desc"
    picture = GFX_evt_warm_barren
    show_sound = event_scanner
    location = from
    is_triggered_only = yes
    option = {
        name = INTERESTING
        from = {
            hidden_effect = {
                if = {
                    limit = {
                        has_deposit_for = shipclass_research_station
                    }
                    clear_deposits = yes
                }
                add_modifier = {
                    modifier = mineral_rich
                    days = -1
                }
            }
            add_deposit = d_minerals_5
        }
    }
    option = {
        name = distar.180.name.b
        trigger = {
            FROM = {
                can_be_terraforming_candidate = yes
            }
            leader = {
                OR = {
                    has_trait = leader_trait_expertise_new_worlds
                    has_trait = leader_trait_spark_of_genius
                    has_trait = leader_trait_maniacal
                }
            }
        }
        allow = {
            owner = {
                resource_stockpile_compare = {
                    resource = energy
                    value >= 1000
                }
            }
        }
        custom_tooltip = distar.180.name.b.tooltip
        owner = {
            add_resource = {
                energy = -1000
            }
        }
        FROM = {
            enable_special_project = {
                name = "THIRSTY_ROCK_PROJECT"
                location = this
                owner = root.owner
            }
        }
    }
}
